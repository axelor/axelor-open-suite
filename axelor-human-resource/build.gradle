plugins {
	id 'com.axelor.app'
	id "com.github.node-gradle.node"
}

apply from: "../libs.gradle"
apply from: "../version.gradle"

apply {
	version = openSuiteVersion
}

axelor {
	title "Axelor Human Resource"
	description "Axelor Human Resource Module"
}

dependencies {
	api project(":modules:axelor-project")
	api project(":modules:axelor-bank-payment")

	implementation libs.commons_lang3
	implementation libs.commons_collections4
	testImplementation libs.mockito
}

node {
	version = '16.17.0'
	yarnVersion = '1.22.19'
    download = true
    distBaseUrl = null
    nodeModulesDir = file('src/main/axelor-react-timesheet')
}

task buildFront(type: YarnTask) {
    group = 'axelor'
    description = 'Build Front-end'

    inputs.files('src/main/axelor-react-timesheet/package.json', 'src/main/axelor-react-timesheet/yarn.lock')
    inputs.dir('src/main/axelor-react-timesheet/src')
    inputs.dir('src/main/axelor-react-timesheet/public')
    inputs.dir(fileTree("src/main/axelor-react-timesheet/.yarn"))
    outputs.dir('src/main/axelor-react-timesheet/build')

	dependsOn 'installFrontDeps'
    args = ["run", "build"]
}

task installFrontDeps(type: YarnTask) {
    group = 'axelor'
    description = 'Install Front-end dependencies'

    args = ['install']
}


task copyReactWebapp(type: Copy) {
	destinationDir = file(rootProject.buildDir)
	into("webapp/hr/timesheet") {
		from "src/main/axelor-react-timesheet/build"
	}
	
	dependsOn 'buildFront'
}


clean {
    delete "src/main/axelor-react-timesheet/.yarn/cache",
     "src/main/axelor-react-timesheet/.yarn/unplugged",
      "src/main/axelor-react-timesheet/.yarn/install-state.gz",
       "src/main/axelor-react-timesheet/dist",
        "src/main/axelor-react-timesheet/node_modules",
        "src/main/axelor-react-timesheet/build"
}

jar {
  dependsOn 'buildFront'
  into('webapp/hr/timesheet') {
    from "src/main/axelor-react-timesheet/build"
    duplicatesStrategy 'exclude'
  }
}


build.dependsOn copyReactWebapp
rootProject.tasks.war.dependsOn copyReactWebapp
