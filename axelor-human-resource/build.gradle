plugins {
  id 'com.axelor.app'
  id "com.github.node-gradle.node"
}

apply from: "../libs.gradle"
apply from: "../version.gradle"

apply {
  version = openSuiteVersion
}

axelor {
  title "Axelor Human Resource"
  description "Axelor Human Resource Module"
}

dependencies {
  api project(":modules:axelor-project")
  api project(":modules:axelor-bank-payment")

  implementation libs.commons_lang3
  implementation libs.commons_collections4
  testImplementation libs.mockito
    implementation libs.swagger_jaxrs
    implementation libs.jaxb_bind_api
}

ext {
  reactDir = 'axelor-react-timesheet'
}

node {
  version = '18.12.0'
  yarnVersion = '1.22.19'
  pnpmVersion = '9.15.9'
    download = true
    distBaseUrl = null
    nodeModulesDir = file(reactDir)
}

task buildFront(type: PnpmTask) {
    group = 'axelor'
    description = 'Build Front-end'

    inputs.files("${reactDir}/package.json", "${reactDir}/package-lock.yaml")
    inputs.dir("${reactDir}/src")
    outputs.dir("${reactDir}/dist")

  dependsOn 'installFrontDeps'
    args = ["run", "build"]
}

task installFrontDeps(type: PnpmTask) {
    group = 'axelor'
    description = 'Install Front-end dependencies'

    args = ['install']
}


task copyReactWebapp(type: Copy) {
  destinationDir = file("$rootProject.buildDir/webapp")
  into("hr/timesheet") {
    from "${reactDir}/dist"
  }
   into("/assets") {
        from "${reactDir}/dist/assets"
    }
  dependsOn 'buildFront'
}


clean {
  delete "${reactDir}/dist",
         "${reactDir}/node_modules",
         "${reactDir}/build"
}

jar {
  dependsOn 'buildFront'
  into('webapp/hr/timesheet') {
    from "${reactDir}/dist"
    duplicatesStrategy 'exclude'
  }
}


build.dependsOn copyReactWebapp
rootProject.tasks.war.dependsOn copyReactWebapp